' *************************** PLC Cutouch CT1720 programma ***************************************
' Written by Michael Devloo
' Written on 28/03/2017
' Version 1.0.0 - Initial design for the MDE Shutter system.
' ************************************************************************************************
'
Const Device = CT1720
Ramclear
Set Debug On

' *********** Main Initialisation ********************
Dim StringByte As String
Dim stringByteTmp As String

' ** Input counter **
Dim iocounter As Byte

' ** Backlight **
Dim Clock As Integer					'Artificial Clock
Dim SavedTime As Integer				'Variable to save current time
Dim LightOff As Byte					'Backlight Status (0 or 1)

' ** Touchscreen **
Dim TX1 As Integer 
Dim TY1 As Integer
Dim upStatusForTouch As Byte
Dim downStatusForTouch As Byte
Dim menuselected As Integer
Dim menuselectedprevious As Integer	

' Save the serial output as string for now.
Dim statusofshutterupword As String
Dim statusofshuttersdownword As String
Dim statusofshutterstotal As String

' ** EEPROM **
Dim r As Byte 'read result
Dim w As Byte 'write result
w = 0
Dim eepromAddress As Integer
eepromAddress = &b10100000
Dim eepromAddressRead As Integer
eepromAddressRead = &b10100001

' ** EEPROM addresses **

#define startAddressUpRT 0
#define endAddressUpRT 24
#define startAddressDownRT 25
#define endAddressDownRT 49
#define startAddressTotal 50
#define endAddressTotal 74

#define wearlevel 99

Dim wearleveloffset As Integer
wearleveloffset = 0

' IF THEY ARE FF -> NOT OK MOVE TO THE NEXT ONE
Set I2c 9,8

' Message to the HES
Dim message As String
message = "Desynchronized"

' Room definitions
Dim prequal As String
prequal = "SHUTTER"

'This is the room definition. Lights inpu
'If there are more than one shutter In the room., extra index
Dim achterkamer As String
achterkamer = "56"

Dim slaapkamer As String
slaapkamer = "57"

Dim keuken As String
keuken = "60"

Dim livingTuin As String
livingTuin = "630"

Dim livingTerras As String
livingTerras = "631"

Dim livingVoordeur As String
livingVoordeur = "632"

Dim garage As String
garage = "67"

Dim badkamerGroot As String
badkamerGroot = "690" 

Dim badkamerKlein As String
badkamerKlein = "691"

Dim bovenKamerWork As String
bovenKamerWork = "75"

Dim bovenSlaapkamer As String
bovenSlaapkamer = "79"

' Piezo definition
#define shortbeep Pulsout 18,150 ' Send out short beep to piezo

' Input definition
#define lowestInput 56
#define highestInput 57
#define waitForEMK 30
#define rolluikomhoogachterkamer 141
#define rolluikomlaagachterkamer 134
#define rolluikomhoogslaapkamer 200
#define rolluikomlaagslaapkamer 250

#define laagsteInput 56
#define hoogsteInput 59


Delay 200
'-------------------------------------
#Include "PindefShutters.INC"
'--------------------------------------
SerialCommunication

_D(200) = waitForEMK
_D(201) = rolluikomhoogachterkamer
_D(202) = rolluikomlaagachterkamer
_D(203) = rolluikomhoogslaapkamer
_D(204) = rolluikomlaagslaapkamer

Set Outonly On
Set Ladder On  

On Ladderint Gosub DoLadderInt
On Timer(10) Gosub EVERY100ms		' Set Timer interrupt to every 100ms

' Touch screen intialization
Set Pad 0,4,20     ''Mode 0, Packet size 4 bytes, Buffer 20 bytes

Drawscreen
On Pad Gosub TouchScreen

' Check EEPROM OFFSET
'CheckEEPROMOffSet

' Read the EEPROM values to correctly initialize.
'ReadI2C startAddressUpRT
'ReadI2C startAddressUpRT +1

Do
Loop

'------- Ladder Interrupt by F40 -----------------------
DoLadderInt:

Calculation achterkamer, 0, 2, 3, 4, 6, 7, 8, rolluikomhoogachterkamer, rolluikomlaagachterkamer
Calculation slaapkamer, 10, 12, 13, 14, 16, 17, 18, rolluikomhoogslaapkamer, rolluikomlaagslaapkamer

Return

'------- Serial Interrupt by Serial port -----------------------
SERIALINTERRUPT:
' *** Serial interrupt ***!
Debug "Response: "
	Do While Blen(1,0) > 0   ' While buffer is not empty then
		' Modify here what needs to be done!!
		stringByteTmp = Getstr(1,1000)
		If (stringByteTmp <> "<") Then
		StringByte = StringByte + stringByteTmp
		Else				
			'check to trigger the ALL UIT	of ALLES AAN.
			If (Val(StringByte) = 1000) Then
			Debug "All Up"
		 	AllUp
			Elseif (Val(StringByte) = 2000) Then			
			Debug "ALL Down"
			AllDown				
			Else 'Verify if its is valid.
				For iocounter = laagsteInput To hoogsteInput
					If Val(StringByte) = iocounter Then
					Debug "Valid value"
					_M(iocounter) = 1
					End If	
				Next		
			End If 
			
		StringByte = ""
		End If
		Debug StringByte, " "
   Loop
   Debug Cr 'New Line
Return

'------- Timer to screen backlight -----------------------
EVERY100ms:
	Incr Clock
	'Calculate elapsed time since last touch. If greater than 20 sec, then turn off backlight
	If Clock-SavedTime>200 Then 	'200 * 100ms = 20 sec
		LightOff=1
		Light 0
	End If
Return

'------- Touchscreen Interrupt by User -----------------------
TouchScreen:
	Set OnPad Off
	TX1 = Getpad(2)    	''First 2 bytes is the x coordinate
	TY1 = Getpad(2)    	'Second 2 bytes is y coordinate
 	If LightOff=1 Then	'Check if light if OFF
		SavedTime=Clock	'Reset Time values.
		LightOff=0			'Set LightOff variable to 0
		Light 1				'If light is OFF, then turn it ON
	Else						'If light is ON, then process 											
		SavedTime=Clock	'Save current time
	End If
	
  ' **** All shutters down button *****
    If Menucheck(97, TX1, TY1) = 1 Then
	    shortbeep 
 	    AllUp
  ' **** All shutters up button *****
    Elseif Menucheck(98, TX1, TY1) = 1 Then
	   shortbeep 
 	   AllDown	
	 Elseif Menucheck(100, TX1, TY1) = 1 Then
	   shortbeep 
		_M(upStatusForTouch) = 1
		' Set here what to do depending of the other touch screen status		
	 Elseif Menucheck(101, TX1, TY1) = 1 Then
	   shortbeep 
		_M(downStatusForTouch) = 1
    Else 
     CheckTouchScreen 	
    End If 	
Set OnPad On
Return

'------- I2C Interrupt -----------------------
ERRORPROC:
Debug "Error i2c !"
Dim erroreeprom As String
erroreeprom = "I2C"
message = "EepromError"
SendMessage message
Return

'------------------ End of the program. -------------------------
End


'**************** SERIAL COMMUNICATION! ****************

Sub SerialCommunication()
 Opencom 1,19200,3,80,80
 Set Rs232 1, 19200, 3
 Putstr 1,"SHUTTER PLC ONLINE", Chr(13), Chr(10)
 SendMessage message
 On Recv1 Gosub SERIALINTERRUPT ' Data Receive Interrupt routine
End Sub

Sub SendOutSerial(shutter As String, up As String, down As String, total As String)
' Put it on the serial port. 
Putstr 1, prequal, "*", shutter, "=", up , "-", down, "-", total, Chr(13), Chr(10)
'CheckAndWriteEEPROM Val(shutter), Val(up), Val(down), Val(total)

End Sub

'**************** STATUS CALCULATION! ****************

Sub Calculation(room As String, counterIncrease As Byte, statusup As Byte, previousStatusUp As Byte, counterDown As Byte, statusdown As Byte, previousStatusDown As Byte, percentageStatus As Byte, shutterUpCalibration As Integer, shutterDownCalibration As Integer)

If _D(previousStatusUp) > _D(statusUp) Or _D(previousStatusDown) > _D(statusDown) Then
	' When status needs to be synched (when the shutter is done)
	SendOutSerial room, Hex _D(statusUp), Hex _D(statusDown), Hex _D(percentageStatus)
	Debug "Up ", Dec _D(statusUp), "% - ", "Down ", Dec _D(statusDown), "% - ", "Status ", Dec _D(percentageStatus), "% ", room, Cr
	_D(previousStatusUp) = 0
	_D(previousStatusDown) = 0
End If

If _D(counterIncrease) > 0 Or _D(counterDown) > 0 Then
_D(statusUp) = (_D(counterIncrease) * 10) / shutterUpCalibration
_D(statusDown) = (_D(counterDown) * 10) / shutterDownCalibration
 
 If _D(previousStatusUp) <> _D(statusUp) Or _D(previousStatusDown) <> _D(statusDown) Then
 	SendOutSerial room, Hex _D(statusUp), Hex _D(statusDown), Hex _D(percentageStatus)
	Debug "Up ", Dec _D(statusUp), "% - ", "Down ", Dec _D(statusDown), "% - ", "Status ", Dec _D(percentageStatus), "% ", room, Cr
 	_D(previousStatusUp) = _D(statusUp)
 	_D(previousStatusDown) = _D(statusDown)
 Endif
Endif

End Sub

'**************** All SHUTTERS DOWN! ****************
Sub AllDown()
message = "UPSynchronizing"
SendMessage message
For iocounter = laagsteInput To hoogsteInput
  	If ((iocounter / 2) * 2 <> iocounter) Then
		If _M(iocounter) <> 1 Then
			_M(iocounter) = 1
			 Debug "allup"
		End If	
  	End If
Next		
End Sub

'**************** All SHUTTERS UP! ****************
Sub AllUp()
message = "DownSynchronizing"
SendMessage message
For iocounter = laagsteInput To hoogsteInput
  	If ((iocounter / 2) * 2 = iocounter) Then
		If _M(iocounter) <> 1 Then
			_M(iocounter) = 1
			 Debug "alldown"
		End If	
  	End If
Next	
End Sub

''**************** WHICH BUTTON PRESSED?! '****************
Sub CheckTouchScreen()
Glocate 20,110
Debug "Checking touchscreen"
   If Menucheck(Val(achterkamer),TX1,TY1) = 1 Then 
		upStatusForTouch = 56
		downStatusForTouch = 57
		menuselected = Val(achterkamer)
		ReverseMenu
	Elseif Menucheck(Val(slaapkamer),TX1,TY1) = 1 Then 
		upStatusForTouch = 58
		downStatusForTouch = 59
		menuselected = Val(slaapkamer)
		ReverseMenu
	Elseif Menucheck(Val(keuken),TX1,TY1) = 1 Then 
		upStatusForTouch = 60
		downStatusForTouch = 61
		menuselected = Val(keuken)		
		ReverseMenu
	Elseif Menucheck(Val(livingTuin),TX1,TY1) = 1 Then 
		upStatusForTouch = 62
		downStatusForTouch = 63		
		menuselected = Val(livingTuin)
		ReverseMenu	
	Elseif Menucheck(Val(livingTerras),TX1,TY1) = 1 Then 
		upStatusForTouch = 64
		downStatusForTouch = 65			
		menuselected = Val(livingTerras)	
		ReverseMenu
	Elseif Menucheck(Val(livingVoordeur),TX1,TY1) = 1 Then 
		upStatusForTouch = 66
		downStatusForTouch = 67	
		menuselected = Val(livingVoordeur)	
		ReverseMenu
	Elseif Menucheck(Val(garage),TX1,TY1) = 1 Then 
		upStatusForTouch = 68
		downStatusForTouch = 69			
		menuselected = Val(garage)	
		ReverseMenu
	Elseif Menucheck(640,TX1,TY1) = 1 Then ' CHANGED THE NUMBER, WAS NOT WORKING! took a random nr
		upStatusForTouch = 70
		downStatusForTouch = 71			
		menuselected = 640
		ReverseMenu
	Elseif Menucheck(641,TX1,TY1) = 1 Then ' CHANGED THE NUMBER, WAS NOT WORKING took a random nr
		upStatusForTouch = 72
		downStatusForTouch = 73
		menuselected = 641
		ReverseMenu
	Elseif Menucheck(Val(bovenKamerWork),TX1,TY1) = 1 Then 
		upStatusForTouch = 74
		downStatusForTouch = 75	
		menuselected = Val(bovenKamerWork)	
		ReverseMenu
	Elseif Menucheck(Val(bovenSlaapkamer),TX1,TY1) = 1 Then 
		upStatusForTouch = 76
		downStatusForTouch = 77				
		menuselected = Val(bovenSlaapkamer)	
		ReverseMenu
	Endif 
End Sub

''**************** Send mesage trough serial to show the end user?! '****************
Sub SendMessage(messageSerial As String)
	SendOutSerial messageSerial, 0, 0, 0
End Sub


''**************** OFFSET TO CHECK THE WEARLEVEL OF THE EEPROM (Min 1M read cycles...)?! '****************
Sub CheckEEPROMOffSet() 

ReadI2C wearlevel
If (w = 1) Then
	wearleveloffset = 100 'IMPROVE STILL DOES NOT LOOP THROUGH
End If

Debug "Wear level ", Dec w
Dim eepromwearlevel As String
eepromwearlevel = "EEPROMWEARLEVEL"
SendOutSerial eepromwearlevel , w, w, w

End Sub

''**************** WHICH ROOM FOR THE EEPROM VALUE?! '****************
Sub CheckAndWriteEEPROM(roomValue As Byte, statusUp As Byte, statusDown As Byte, totalStatus As Byte)
	Dim index As Byte
	index = 255

	If Val(achterkamer) = roomValue Then
		index = 0
	Elseif Val(slaapkamer) = roomValue Then 
		index = 1
	Elseif Val(keuken) = roomValue Then 
		index = 2
	Elseif Val(livingTuin) = roomValue Then 
		index =  3
	Elseif Val(livingTerras) = roomValue Then 
		index = 4
	Elseif Val(livingVoordeur) = roomValue Then 
		index = 5
	Elseif Val(garage) = roomValue Then 
		index = 6
	Elseif 640 = roomValue Then 
		index = 7
	Elseif 641 = roomValue Then 
		index = 8
	Elseif Val(bovenKamerWork) = roomValue Then 
		index = 9
	Elseif Val(bovenSlaapkamer) = roomValue Then 
		index = 10
	Endif 
		
	If index <> 255 Then
		Debug "EEPROM Offset ", Dec wearleveloffset, "-", Dec index, Cr
		Debug "Writing ", Hex statusUp, "-", Hex statusDown, "-", Hex totalStatus, Cr
		WriteI2C wearleveloffset + startAddressUpRT + index, statusUp
	   WriteI2C wearleveloffset + startAddressDownRT + index , statusDown
		WriteI2C wearleveloffset + startAddressTotal + index, totalStatus
	Endif
End Sub


''**************** REVERSE MENU! '****************
Sub ReverseMenu()
If menuselected <> 0 Then
		Menureverse menuselected
		If menuselectedprevious <> 0 Then
			Menureverse menuselectedprevious
		End If
		menuselectedprevious = menuselected	
	End If	
End Sub

''**************** WRITE TO EEPROM! '****************
Sub WriteI2C(address As Integer, writeData As Byte)
I2cstart
If I2cwrite(eepromAddress)= 1 Then Gosub ERRORPROC 'Chip address = A0 (A= hardcoded for the 24LC256)
r=I2cwrite(address.byte1)' HIGH Address to write to
r=I2cwrite(address.lowbyte)'LOW Address to write to
r=I2cwrite(writeData)'Data to write
I2cstop 'Stop

Delay 10

End Sub

''**************** READ EEPROM! '****************
Sub ReadI2C(address As Integer)

' Read 1 Byte
I2cstart
If I2cwrite(eepromAddress) = 1 Then Gosub ERRORPROC	'Chip address = A0 (A = hardcoded for the 24LC256)
w=I2cwrite(address.Byte1)			' HIGH Address
w=I2cwrite(address.LowByte)		'LOW Address
I2cstart 								'Repeated START
w=I2cwrite(eepromAddressRead)		'Send Read command
w=I2cread(0) 							'Store result into a
I2cstop

' Print Results
Debug Hex w,Cr
Delay 10

End Sub

#Include "MAINSCREENSHUTTERS.INC"